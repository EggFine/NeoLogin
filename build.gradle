plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = 'com.blbilink'
def baseVersion = "1.21.8"
// 从环境变量读取 CI 提供的构建号
def buildNumber = System.getenv("GITHUB_RUN_NUMBER") ?: "SNAPSHOT"
version = "${baseVersion}.${buildNumber}"

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "sonatype-oss-snapshots1"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    // GeyserMC/Floodgate 仓库
    maven {
        name = "opencollab-snapshot"
        url = "https://repo.opencollab.dev/main/"
    }
}

dependencies {
    compileOnly "dev.folia:folia-api:1.21.8-R0.1-SNAPSHOT"
    implementation 'at.favre.lib:bcrypt:0.10.2'
    implementation fileTree(dir: 'libs', include: '*.jar')
    // Floodgate API (基岩版支持)
    compileOnly 'org.geysermc.floodgate:api:2.2.4-SNAPSHOT'
    // Cumulus (Floodgate 表单 API)
    compileOnly 'org.geysermc.cumulus:cumulus:1.1.2'
}

tasks {
    runServer {
        
    }
    
    shadowJar {
        // 重新定位 BCrypt 包以避免与其他插件冲突
        relocate 'at.favre.lib.crypto.bcrypt', 'com.blbilink.neoLogin.lib.bcrypt'
        
        // 设置分类器为空，这样生成的文件名就是 plugin-version.jar 而不是 plugin-version-all.jar
        archiveClassifier = ''
        
        // 排除不必要的文件
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
    }
    
    // 确保 shadowJar 任务在 build 时执行
    build {
        dependsOn shadowJar
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
